plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    compileSdk 34
    //ndkVersion flutter.ndkVersion

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        applicationId "app.kitadai31.manager.flutter"
        minSdk 21
        targetSdk 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "ReVanced Manager A5-7"
            signingConfig signingConfigs.debug
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a'
            }
        }
        debug {
            shrinkResources false
            minifyEnabled false
            resValue "string", "app_name", "ReVanced Manager Debug A5-7"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        profile {
            shrinkResources false
            minifyEnabled false
            resValue "string", "app_name", "ReVanced Manager Profile"
            applicationIdSuffix ".profile"
            signingConfig signingConfigs.debug
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
            excludes += ['/prebuilt/**']
        }
        resources {
            excludes += ['/prebuilt/**']
        }
    }

    namespace 'app.revanced.manager.flutter'
}

kotlin {
    jvmToolchain(17)
}

flutter {
    source '../..'
}

dependencies {
    // ReVanced
    // Switch to a fork to improve performance
    // to build the app, refer to this page
    // https://github.com/kitadai31/revanced-patcher/releases/tag/v14.2.2-2
    implementation "app.revanced:revanced-patcher:14.2.2-2"

    // Signing & aligning
    implementation("org.bouncycastle:bcpkix-jdk15on:1.70")
    implementation("com.github.MuntashirAkon:apksig-android:4.4.0")

    // Android <8 support
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
}
